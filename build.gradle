buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }                      // Mirrors jcenter() and mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:2.1.2"                    // Android gradle plugin
        classpath "com.novoda:gradle-android-command-plugin:1.5.0"          // Android command plugin
        classpath "com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.5.4"   // Dex method count
        classpath "com.vanniktech:gradle-android-apk-size-plugin:0.2.0"     // Apk size
        classpath "me.tatarka:gradle-retrolambda:3.2.5"                     // Java 8 lambda support
        classpath "me.tatarka.retrolambda.projectlombok:lombok.ast:0.2.3.a2"
        classpath "com.neenbedankt.gradle.plugins:android-apt:1.8"          // Annotation processing
    }

    // Retrolambda - exclude the version that the android plugin depends on
    configurations.classpath.exclude group: "com.android.tools.external.lombok"
}

plugins {
    id "com.gradle.build-scan" version "1.0"                                // Gradle build receipt
    id "net.ltgt.errorprone" version "0.0.8"                                // Google error-prone
    id "com.github.kt3k.coveralls" version "2.6.3"                          // Code coverage
    id "com.github.ben-manes.versions" version "0.12.0"                     // Check for plugin updates
    id "build-dashboard"
    id "jacoco"
}

repositories {
    maven { url "https://plugins.gradle.org/m2/" }                          // Mirrors jcenter() and mavenCentral()
    maven { url "https://jitpack.io" }
}

apply plugin: "com.android.application"
apply plugin: "android-command"
apply plugin: "com.getkeepsafe.dexcount"
apply plugin: "com.vanniktech.android.apk.size"
apply plugin: "me.tatarka.retrolambda"
apply plugin: "com.neenbedankt.android-apt"
apply from: rootProject.file("gradle/buildScan.gradle")
apply from: rootProject.file("gradle/compile.gradle")
apply from: rootProject.file("gradle/codeQuality.gradle")

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.0"

    defaultConfig {
        applicationId "burrows.apps.giphy.example"
        minSdkVersion 11
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"

        vectorDrawables {
            useSupportLibrary = true
        }

        // For espresso tests
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        debug {
            storeFile rootProject.file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles(getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro")
            signingConfig signingConfigs.debug
        }
    }

    lintOptions {
        abortOnError true
        textReport true
        textOutput "stdout"
        checkAllWarnings true
        warningsAsErrors true
        showAll true
        explainIssues true
        disable "UnusedResources",              // Unused will be removed on release
                "IconExpectedSize",             // Using the material icons provided from Google
                "GoogleAppIndexingApiWarning",  // We might want to index our app later
                "InvalidPackage",               // Butterknife, Okio and Realm
                "ResourceType",                 // Annotation binding
                "GradleDependency",
                "NewerVersionAvailable",
                "PropertyEscape"                // CI issue with sdk.dir in local.properties
    }

    testOptions {
        unitTests {
            all {
                jacoco {
                    includeNoLocationClasses = true
                }
            }
        }
    }
}

dependencies {
    compile "com.android.support:design:24.0.0"
    compile "com.android.support:cardview-v7:24.0.0"
    compile "com.jakewharton:butterknife:8.1.0"
    apt "com.jakewharton:butterknife-compiler:8.1.0"
    compile "com.squareup.okhttp3:logging-interceptor:3.3.1"
    compile "com.squareup.okhttp3:okhttp:3.3.1"
    compile "com.squareup.retrofit2:adapter-rxjava:2.1.0"
    compile "com.squareup.retrofit2:converter-gson:2.1.0"
    compile "com.squareup.retrofit2:retrofit:2.1.0"
    compile "io.reactivex:rxandroid:1.2.1"
    compile "io.reactivex:rxjava:1.1.6"
    compile "com.felipecsl:gifimageview:2.0.0"
    compile "com.github.bumptech.glide:glide:3.7.0"
    compile "com.github.bumptech.glide:okhttp3-integration:1.4.0@aar"
    compile "org.greenrobot:eventbus:3.0.0"

    androidTestCompile "com.android.support:support-annotations:24.0.0"
    androidTestCompile "com.android.support:design:24.0.0"
    androidTestCompile "com.android.support.test:rules:0.5"
    androidTestCompile("com.android.support.test.espresso:espresso-contrib:2.2.2") {
        exclude module: "appcompat"
        exclude module: "support-v4"
        exclude module: "recyclerview-v7"
        exclude module: "support-annotations"
    }

    testCompile "junit:junit:4.12"
    testCompile "org.mockito:mockito-core:2.0.69-beta"
    testCompile "org.assertj:assertj-core:3.4.1"
    testCompile "org.robolectric:shadows-support-v4:3.1"
    testCompile "org.khronos:opengl-api:gl1.1-android-2.1_r1"
    testCompile "com.squareup.retrofit2:retrofit-mock:2.1.0"
    testCompile "com.squareup.okhttp3:mockwebserver:3.3.1"
}
